; parse variable
(define (variable? x)
  (symbol? x))
(define (same-variable? v1 v2)
  (and (variable? v1) (variable? v2) (eq? v1 v2)))
; make
(define (=number? exp num)
  (and (number? exp) (= exp num)))
(define (make-sum a1 a2)
  (cond ((=number? a1 0) a2)
        ((=number? a2 0) a1)
        ((and (number? a1) (number? a2)) (+ a1 a2))
        (else (list '+ a1 a2))))
(define (make-product m1 m2)
  (cond ((or (=number? m1 0) (=number? m2 0)) 0)
        ((=number? m1 1) m2)
        ((=number? m2 1) m1)
        ((and (number? m1) (number? m2)) (* m1 m2))
        (else (list '* m1 m2))))
; parse sum
(define (sum? x)
  (and (pair? x) (eq? (car x) '+)))
(define (addend s)
  (car (cdr s)))
(define (augend s)
  (cond ((null? (cdr (cdr (cdr s)))) (car (cdr (cdr s))))
        (else (append (list '+ ) (cdr (cdr s))))))
; parse product
(define (product? x)
  (and (pair? x) (eq? (car x) '*)))
(define (multiplier p)
  (car (cdr p)))
(define (multiplicand p)
  (cond ((null? (cdr (cdr (cdr p)))) (car (cdr (cdr p))))
        (else (append (list '*) (cdr (cdr p))))))
; exponentiation
(define (exponentiation? e)
  (and (pair? e) (eq? (car e) '**) (number? (car (cdr (cdr e))))))
(define (base e)
  (car (cdr e)))
(define (exponent e)
  (car (cdr (cdr e))))
(define (make-exponentiation b e)
  (cond ((=number? e 0) 1)
        ((=number? e 1) b)
        (else (list '** b e))))
; deriv
(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp)
         (if (same-variable? exp var) 1 0))
        ((sum? exp)
         (make-sum (deriv (addend exp) var)
                   (deriv (augend exp) var)))
        ((product? exp)
         (make-sum
          (make-product (multiplier exp)
                        (deriv (multiplicand exp) var))
          (make-product (deriv (multiplier exp) var)
                        (multiplicand exp))))
        ((exponentiation? exp)
         (make-product (make-product (exponent exp)
                                     (make-exponentiation (base exp)
                                                          (- (exponent exp) 1)))
                       (deriv (base exp) var)))
        (else
         (error "unkown expression type -- DERIV" exp))))
; test
(deriv '(+ x 3) 'x)
(deriv '(* x y) 'x)
(deriv '(* (* x y) (+ x 3)) 'x)
(deriv '(* (* y x) 3) 'x)
(deriv '(+ (* a x) (* b x) (* c x) (* d x)) 'x)
(deriv '(* x y (+ x 3)) 'x)